cmake_minimum_required(VERSION 2.4.7)
SET(cmake_verbose_makefile TRUE)

IF(COMMAND CMAKE_POLICY)
  CMAKE_POLICY(SET CMP0003 NEW)
ENDIF(COMMAND CMAKE_POLICY)


INCLUDE(CheckLibraryExists)
INCLUDE(CheckIncludeFile)
INCLUDE (CheckIncludeFiles)

PROJECT(zeroxml C)

OPTION(UTILS  "Build and install utility programs"       ON)
OPTION(WERROR "Treat compile warnings as errors"         OFF)
OPTION(NODECACHE "Use XML Node cache"                    OFF)
OPTION(NONVALIDATING "Trun off XML validation"           OFF)

IF(WIN32)
  SET(LIBZEROXML ZeroXML32)
  SET(LIBZEROXML_DEBUG ZeroXML32-rmalloc)
  ADD_DEFINITIONS("-D_WIN32")
  ADD_DEFINITIONS("-DXML_BUILD_LIBRARY=1")
  OPTION(MEMDEBUG "Turn on memory debugging functions"     OFF)
ELSE()
  SET(LIBZEROXML zeroxml)
  SET(LIBZEROXML_DEBUG zeroxml-rmalloc)
  OPTION(MEMDEBUG "Turn on memory debugging functions"     ON)
ENDIF()

SET(LIBTYPE STATIC)

# detect system type
IF(NOT DEFINED CPACK_SYSTEM_NAME)
  SET(CPACK_SYSTEM_NAME ${CMAKE_SYSTEM_NAME})
ENDIF(NOT DEFINED CPACK_SYSTEM_NAME)
IF (UNIX AND NOT WIN32)
  IF(CMAKE_SIZEOF_VOID_P EQUAL 8)
    SET(CPACK_PACKAGE_ARCHITECTURE "x86_64")
  ELSE(CMAKE_SIZEOF_VOID_P EQUAL 8)
    SET(CPACK_PACKAGE_ARCHITECTURE "i386")
  ENDIF(CMAKE_SIZEOF_VOID_P EQUAL 8)
ENDIF(UNIX AND NOT WIN32)

# read 'version' file into a variable (stripping any newlines or spaces)
file(READ version versionFile)
string(STRIP ${versionFile} ZEROXML_VERSION)
SET(VERSION ZEROXML_VERSION)

#  read 'description` file into a variable
file(STRINGS description descriptionFile)
STRING(REGEX REPLACE ";" " " descriptionFile "${descriptionFile}")

# split version string into components, note CMAKE_MATCH_0 is the entire regexp match
STRING(REGEX MATCH "([0-9]+)\\.([0-9]+)\\.([0-9]+)" CPACK_PACKAGE_VERSION ${ZEROXML_VERSION})
SET(CPACK_PACKAGE_VERSION_MAJOR ${CMAKE_MATCH_1})
SET(CPACK_PACKAGE_VERSION_MINOR ${CMAKE_MATCH_2})
SET(CPACK_PACKAGE_VERSION_PATCH ${CMAKE_MATCH_3})
SET(CPACK_PACKAGE_NAME "zeroxml")
SET(CPACK_PACKAGE_FILE_NAME "${CPACK_PACKAGE_NAME}-${CPACK_PACKAGE_VERSION}.el6.${CPACK_PACKAGE_ARCHITECTURE}")
SET(CPACK_PACKAGE_CONTACT "tech@adalin.org")
SET(CPACK_PACKAGE_VENDOR "tech@adalin.org")
SET(CPACK_PACKAGE_DESCRIPTION "A lightweight, cross platform XML configuration file reading library")
SET(CPACK_PACKAGE_DESCRIPTION_SUMMARY ${descriptionFile})
SET(CPACK_PACKAGE_DESCRIPTION_FILE "${PROJECT_SOURCE_DIR}/description")
SET(CPACK_RESOURCE_FILE_LICENSE "${PROJECT_SOURCE_DIR}/COPYING")
SET(CPACK_RESOURCE_FILE_README "${PROJECT_SOURCE_DIR}/README")

# DEBIAN
IF(CMAKE_SIZEOF_VOID_P EQUAL 8)
  SET(CPACK_DEBIAN_PACKAGE_ARCHITECTURE "amd64")
ELSE(CMAKE_SIZEOF_VOID_P EQUAL 8)
  SET(CPACK_DEBIAN_PACKAGE_ARCHITECTURE "i386")
ENDIF(CMAKE_SIZEOF_VOID_P EQUAL 8)
SET(CPACK_DEBIAN_PACKAGE_ARCHITECTURE ${PACK_PACKAGE_ARCHITECTURE})
SET(CPACK_DEBIAN_PACKAGE_SECTION "devel")
SET(CPACK_DEBIAN_PACKAGE_DEPENDS "libc6 (>= 2.1)")

# RPM
SET(CPACK_RPM_PACKAGE_ARCHITECTURE ${PACK_PACKAGE_ARCHITECTURE})
SET(CPACK_RPM_PACKAGE_GROUP "System/Libraries")


SET(CPACK_SOURCE_GENERATOR ZIP)
SET(CPACK_SOURCE_PACKAGE_FILE_NAME "${CMAKE_PROJECT_NAME}-${ZEROXML_VERSION}" CACHE INTERNAL "tarball basename")
SET(CPACK_SOURCE_IGNORE_FILES
  "^${PROJECT_BINARY_DIR};${PROJECT_SOURCE_DIR}/.git;\\\\.gitignore;Makefile.am;~$;${CPACK_SOURCE_IGNORE_FILES}")

SET(CPACK_GENERATOR "DEB;RPM")
SET(CPACK_DEBIAN_PACKAGE_MAINTAINER "Adalin B.V.")

INCLUDE (CPack)

# We have some custom .cmake scripts not in the official distribution.
SET(CMAKE_MODULE_PATH "${PROJECT_SOURCE_DIR}/CMakeModules;${CMAKE_MODULE_PATH}")

# Add definitions, compiler switches, etc.
INCLUDE_DIRECTORIES("${CMAKE_SOURCE_DIR}"/src src include "${CMAKE_BINARY_DIR}/include")

IF(NOT CMAKE_BUILD_TYPE)
  SET(CMAKE_BUILD_TYPE Release CACHE STRING
      "Choose the type of build, options are: None Debug Release RelWithDebInfo MinSizeRel."
      FORCE)
ENDIF(NOT CMAKE_BUILD_TYPE)


ADD_DEFINITIONS(-DHAVE_CONFIG_H=1)
IF(WERROR)
  ADD_DEFINITIONS(-Werror)
ENDIF()

IF(MSVC)
    SET(CMAKE_C_FLAGS_RELWITHDEBINFO "${CMAKE_C_FLAGS_RELWITHDEBINFO} -D_DEBUG")
    SET(CMAKE_C_FLAGS_MINSIZEREL "${CMAKE_C_FLAGS_MINSIZEREL} -DNDEBUG")
    SET(CMAKE_C_FLAGS_RELEASE "${CMAKE_C_FLAGS_RELEASE} -DNDEBUG")
    SET(CMAKE_C_FLAGS_DEBUG "${CMAKE_C_FLAGS_DEBUG} -D_DEBUG")
    ADD_DEFINITIONS(-D_CRT_SECURE_NO_WARNINGS)
    ADD_DEFINITIONS(-D_CRT_NONSTDC_NO_DEPRECATE)

    OPTION(FORCE_STATIC_VCRT "Force /MT for static VC runtimes" OFF)
    IF(FORCE_STATIC_VCRT)
        FOREACH(flag_var
                CMAKE_C_FLAGS CMAKE_C_FLAGS_DEBUG CMAKE_C_FLAGS_RELEASE
                CMAKE_C_FLAGS_MINSIZEREL CMAKE_C_FLAGS_RELWITHDEBINFO)
            IF(${flag_var} MATCHES "/MD")
                STRING(REGEX REPLACE "/MD" "/MT" ${flag_var} "${${flag_var}}")
            ENDIF()
        ENDFOREACH(flag_var)
    ENDIF()
ELSE(MSVC)
    ADD_DEFINITIONS(-Wall -fPIC)
    SET(C_FLAGS "${C_FLAGS} -Wall -Werror=format-security" CACHE STRING
        "Flags used by al build types."
        FORCE)
    SET(CMAKE_C_FLAGS_RELWITHDEBINFO "${C_FLAGS} -g -O2 -D_DEBUG" CACHE STRING
        "Flags used by the compiler during Release with Debug Info builds."
        FORCE)
    SET(CMAKE_C_FLAGS_MINSIZEREL "${C_FLAGS} -Os -DNDEBUG" CACHE STRING
        "Flags used by the compiler during release minsize builds."
        FORCE)
    SET(CMAKE_C_FLAGS_RELEASE "${C_FLAGS} -Os -fomit-frame-pointer -DNDEBUG"
        CACHE STRING "Flags used by the compiler during release builds"
        FORCE)
    SET(CMAKE_C_FLAGS_DEBUG "${C_FLAGS} -g3 -D_DEBUG" CACHE STRING
        "Flags used by the compiler during debug builds."
        FORCE)
ENDIF(MSVC)

SET(XML_USE_NODECACHE ${NODECACHE})
SET(XML_NONVALIDATING ${NONVALIDATING})
SET(MALLOC_DEBUG ${MEMDEBUG})

CHECK_INCLUDE_FILE(strings.h HAVE_STRINGS_H)
CHECK_INCLUDE_FILE(unistd.h HAVE_UNISTD_H)

CONFIGURE_FILE(
    "${CMAKE_CURRENT_SOURCE_DIR}/include/config.h.in"
    "${CMAKE_CURRENT_BINARY_DIR}/include/config.h")

SET(ZEROXML_OBJS
    src/xml.c
    src/xml_cache.c
   )

# Build a library
IF(CMAKE_SIZEOF_VOID_P MATCHES "8")
  SET(LIB_POSTFIX "64" CACHE STRING "suffix for 32/64 dir placement")
  MARK_AS_ADVANCED(LIB_POSTFIX)
ENDIF(CMAKE_SIZEOF_VOID_P MATCHES "8")
IF(NOT DEFINED LIB_POSTFIX)
  SET(LIB_POSTFIX "")
ENDIF(NOT DEFINED LIB_POSTFIX)

SET(LIB_VERSION "${CPACK_PACKAGE_VERSION_MAJOR}.${CPACK_PACKAGE_VERSION_MINOR}.${CPACK_PACKAGE_VERSION_PATCH}")
ADD_LIBRARY(${LIBZEROXML} ${LIBTYPE} ${ZEROXML_OBJS})
SET_TARGET_PROPERTIES(${LIBZEROXML} PROPERTIES DEFINE_SYMBOL XML_BUILD_LIBRARY
                                       COMPILE_FLAGS -D_PROTOTYPES
                                       VERSION ${LIB_VERSION}
                                       SOVERSION ${CPACK_PACKAGE_VERSION_MAJOR})
IF(WIN32 AND NOT LIBTYPE STREQUAL "STATIC")
    SET_TARGET_PROPERTIES(${LIBZEROXML} PROPERTIES PREFIX "")
ENDIF()

TARGET_LINK_LIBRARIES(${LIBZEROXML} ${EXTRA_LIBS})

# Add an install target here
INSTALL(TARGETS ${LIBZEROXML}
        RUNTIME DESTINATION bin
        ARCHIVE DESTINATION "lib${LIB_POSTFIX}"
       )

IF(MEMDEBUG)
  SET(ZEROXMLDEBUG_OBJS
      src/rmalloc.c
      src/xml.c
      src/xml_cache.c
     )

  ADD_LIBRARY(${LIBZEROXML_DEBUG} ${LIBTYPE} ${ZEROXMLDEBUG_OBJS})
  SET_TARGET_PROPERTIES(${LIBZEROXML_DEBUG}
                         PROPERTIES DEFINE_SYMBOL XML_BUILD_LIBRARY
                         COMPILE_FLAGS "-D_PROTOTYPES -DMALLOC_DEBUG=ON"
                         VERSION ${LIB_VERSION}
                         SOVERSION ${CPACK_PACKAGE_VERSION_MAJOR})
  IF(WIN32 AND NOT LIBTYPE STREQUAL "STATIC")
    SET_TARGET_PROPERTIES(${LIBZEROXML_DEBUG} PROPERTIES PREFIX "")
  ENDIF()

  TARGET_LINK_LIBRARIES(${LIBZEROXML_DEBUG} ${EXTRA_LIBS})

  # Add an install target here
  INSTALL(TARGETS ${LIBZEROXML_DEBUG}
        RUNTIME DESTINATION bin
        ARCHIVE DESTINATION "lib${LIB_POSTFIX}"
       )
ENDIF(MEMDEBUG)

INSTALL(FILES
        include/rmalloc.h
        include/xml_cache.h
        include/xml.h
        DESTINATION include
       )

IF(UTILS)
  ADD_SUBDIRECTORY(examples)
ENDIF(UTILS)

#-----------------------------------------------------------------------------
### uninstall target
#-----------------------------------------------------------------------------
CONFIGURE_FILE(
  "${CMAKE_CURRENT_SOURCE_DIR}/CMakeModules/cmake_uninstall.cmake.in"
  "${CMAKE_CURRENT_BINARY_DIR}/cmake_uninstall.cmake"
  IMMEDIATE @ONLY)
ADD_CUSTOM_TARGET(uninstall
  "${CMAKE_COMMAND}" -P "${CMAKE_CURRENT_BINARY_DIR}/cmake_uninstall.cmake")

